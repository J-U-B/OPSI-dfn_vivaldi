;===============================================================================
; checkinstance.opsiinc
; J. Boettge <boettge@mpi-halle.mpg.de> 2017-10-27 13:21:45 +0200
;===============================================================================

;===============================================================================
[sub_checkinstance]
;===============================================================================
comment "Checking for running instances and kill it (if allowed)."

DefVar $Proc_Cnt$
DefVar $Proc_Killed$
DefVar $PSDETAIL$
DefVar $Kill_Running$
DefVar $CheckBin$

set $PSDETAIL$ = "%ScriptPath%\bin\psdetail.exe"

set $Kill_Running$ = GetProductProperty("kill_running", "False")

if FileExists($PSDETAIL$) 
	set $CheckBin$ = $MainBin$
	sub_CheckBin
	set $CheckBin$ = $NotifierBin$
	sub_CheckBin
else
	logError "can't find psdetail.exe"
endif

;===============================================================================
[sub_CheckBin]
;===============================================================================
if not ($CheckBin$ = "")
	winbatch_psdetail_count
	set $Proc_Cnt$ = getLastExitCode
	if $Proc_Cnt$ > "0"
		if $Kill_Running$ = "True"
			comment $Proc_Cnt$ + " running instance(s) of " + $CheckBin$ + " found, killing them."
			winbatch_psdetail_kill
			set $Proc_Killed$ = getLastExitCode
			comment $Proc_Killed$ + " instance(s) of " + $CheckBin$ + " were killed."
			if not ($Proc_Cnt$=$Proc_Killed$)
				logWarning "Number of killed instances (" + $Proc_Killed$ + ") does not match expectations (" + $Proc_Cnt$ + ")."
			endif
		else
			logError $Proc_Cnt$+" running instance(s) of " + $CheckBin$ + " found, but I'm not allowed to kill them."
			isFatalError "running instance"
		endif
	else
		if $Proc_Cnt$ = "0"
			comment "no running instance of " + $CheckBin$ + " found"
		else
			logError "psdetail returns " + $Proc_Cnt$ + " (unexpected)"
		endif
	endif
endif

;===============================================================================
[winbatch_psdetail_count]
;===============================================================================
"$PSDETAIL$" -e "$CheckBin$"

;===============================================================================
[winbatch_psdetail_kill]
;===============================================================================
"$PSDETAIL$" -k -e "$CheckBin$"
