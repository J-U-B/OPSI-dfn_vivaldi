;=====================================================================
; delsub.opsiinc VIVALDI
; J. Boettge <boettge@mpi-halle.mpg.de>  2018-07-02 14:16:16 +0200
;=====================================================================

;=======================================
[sub_delsub_main]
;=======================================
;Message "Removing previous version(s) of " + $ProductName$
;ShowBitmap $Img_Prepare$ $ProductName$
marktime

;=== search for uninstall info
Set $UninstRegKeyPath$ = "[" + $MPIMSP_Reg$ + "]"
Set $UninstDir$  = GetRegistryStringValueSysNative($UninstRegKeyPath$ + " InstallDir")
Set $UninstVer$  = GetRegistryStringValueSysNative($UninstRegKeyPath$ + " Version")
Set $UninstArch$ = GetRegistryStringValueSysNative($UninstRegKeyPath$ + " Architecture")
Set $UninstName$ = GetRegistryStringValueSysNative($UninstRegKeyPath$ + " ProductName")

if ($UninstDir$ = "")
	comment "no uninstall info found in regisrty at " + $UninstRegKeyPath$
	comment "trying default folder and architecture: [" + $InstallDir$ + "]"
	set $UninstDir$ = $InstallDir$
endif

if ($UninstName$ = "")
	Set $UninstName$ = $ProductName$
endif

;=== normalize:
if not (strPart($UninstDir$, strLength($UninstDir$),"1") = "\" )
	set $UninstDir$ = $UninstDir$ + "\"
endif


if FileExists($UninstDir$)

	;=== kill running instances
	include_append "%ScriptPath%\checkinstance.opsiinc"
	sub_checkinstance

	
	ShowBitmap $Img_Uninstall$	 $ProductName$
	if ($UninstVer$="") or ($UninstArch$="")
		Message "Uninstalling previous setup of " + $ProductName$
	else
		Message "Uninstalling previous setup of " + $UninstName$ + " (" + $UninstArch$ + " bit)"
	endif


	Message "removing link from startmenu"
	LinkFolder_RemoveVivaldi
	LinkFolder_RemoveDesktopLink
	Message "removing installation directory"
	set $LogLevel$ = getLogLevel
	SetLogLevel=5
	Files_RemoveInstallDir
	SetLogLevel=$LogLevel$

	;if ($pr_MakeDefault$ = "true")
	;	Message "Removing Vivaldi as default handler"
	;	Registry_RemoveVivaldiDefault /sysnative
	;endif
	Message "Removing Vivaldi handler settings from registry"
	Registry_RemoveVivaldiHTM /sysnative
	
	Message "Removing uninstall information"
	Registry_RemoveVivaldiInfo /sysnative
	
	
	;=== custom post uninstall
	comment "include custom post uninstall file"
	if not ($CustomPostUninstall$ = "none")
		if FileExists("%ScriptPath%\custom\" + $CustomPostUninstall$)
			include_insert "%ScriptPath%\custom\" + $CustomPostUninstall$
		endif
	endif
	
	set $TimeDiff$ = getDiffTimeSec
	Message  "Setup time: " + $TimeDiff$ + " seconds"
endif



;=====================================================================
[Files_RemoveInstallDir]
;=====================================================================
del -sf "$UninstDir$"


;=====================================================================
[LinkFolder_RemoveVivaldi]
;=====================================================================
set_basefolder common_programs
set_subfolder ""
;delete_element "$ProductName$"
delete_element $ProductName$


;=====================================================================
[LinkFolder_RemoveDesktopLink]
;=====================================================================
set_basefolder common_desktopdirectory
set_subfolder ""
;delete_element "$ProductName$"
delete_element $ProductName$


;=====================================================================
[Registry_RemoveVivaldiInfo]
;=====================================================================
deletekey [$MPIMSP_Reg$]


;=====================================================================
[Registry_RemoveVivaldiHTM]
;=====================================================================
deleteKey [HKEY_CLASSES_ROOT\VivaldiHTM]
deleteKey [HKEY_LOCAL_MACHINE\SOFTWARE\Clients\StartMenuInternet\Vivaldi]
;deleteKey [HKEY_CLASSES_ROOT\CLSID\{42042206-2D85-11D3-8CFF-005004838597}\Old Icon\VivaldiHTM\DefaultIcon]

openkey [HKEY_LOCAL_MACHINE\SOFTWARE\RegisteredApplications]
deleteVar "Vivaldi"

openkey [HKEY_CLASSES_ROOT\.htm\OpenWithProgIDs]
deleteVar "VivaldiHTM"
openkey [HKEY_CLASSES_ROOT\.html\OpenWithProgIDs]
deleteVar "VivaldiHTM"
openkey [HKEY_CLASSES_ROOT\.xht\OpenWithProgIDs]
deleteVar "VivaldiHTM"
openkey [HKEY_CLASSES_ROOT\.xhtml\OpenWithProgIDs]
deleteVar "VivaldiHTM"
openkey [HKEY_CLASSES_ROOT\.xhtml\OpenWithProgIDs]
deleteVar "VivaldiHTM"
openkey [HKEY_CLASSES_ROOT\.pdf\OpenWithProgIDs]
deleteVar "VivaldiHTM"
openkey [HKEY_CLASSES_ROOT\.svg\OpenWithProgIDs]
deleteVar "VivaldiHTM"
openkey [HKEY_CLASSES_ROOT\.webp\OpenWithProgIDs]
deleteVar "VivaldiHTM"
openkey [HKEY_CLASSES_ROOT\.shtml\OpenWithProgIDs]
deleteVar "VivaldiHTM"


;=====================================================================
[Registry_RemoveVivaldiDefault]
;=====================================================================
openkey [HKEY_CLASSES_ROOT\.htm]
deleteVar ""
openkey [HKEY_CLASSES_ROOT\.html]
deleteVar ""
openkey [HKEY_CLASSES_ROOT\.xht]
deleteVar ""
openkey [HKEY_CLASSES_ROOT\.xhtml]
deleteVar ""
openkey [HKEY_CLASSES_ROOT\.shtml]
deleteVar ""
openkey [HKEY_CLASSES_ROOT\.svg]
deleteVar ""

openkey [HKEY_CLASSES_ROOT\http\DefaultIcon]
deleteVar ""
openkey [HKEY_CLASSES_ROOT\http\shell\open\command]
deleteVar ""

openkey [HKEY_CLASSES_ROOT\https\DefaultIcon]
deleteVar ""
openkey [HKEY_CLASSES_ROOT\https\shell\open\command]
deleteVar ""

openkey [HKEY_CLASSES_ROOT\ftp\DefaultIcon]
deleteVar ""
openkey [HKEY_CLASSES_ROOT\ftp\shell\open\command]
deleteVar ""

openkey [HKEY_LOCAL_MACHINE\SOFTWARE\Clients\StartMenuInternet]
deleteVar ""
